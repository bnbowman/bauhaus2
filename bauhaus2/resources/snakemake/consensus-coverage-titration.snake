
# ---------------------------------------------------------------------------------------------------
# consensus-coverage-titration.snake : gauge genomic consensus performance at fixed coverage levels



masked_variant_calls = \
    { c : expand("conditions/{condition}/variant_calling/masked-variants.{depth}.gff",
                 condition=c, depth=config["bh2.consensus_coverage_titration.depths"])
      for c in ct.conditions }

variant_calls = \
    { c : expand("conditions/{condition}/variant_calling/variants.{depth}.gff",
                 condition=c, depth=config["bh2.consensus_coverage_titration.depths"])
      for c in ct.conditions }

alignments_summaries = \
    { c : expand("conditions/{condition}/variant_calling/alignments-summary.gff", condition=c)
      for c in ct.conditions }


# -- report!

rule coverage_titration_report:
    output: "reports/CoverageTitration/report.json"
    input:
        maskedVariantGffs=masked_variant_calls.values(),
        alignmentSummary=alignments_summaries.values()
    shell:
        """
        Rscript --vanilla scripts/R/coverageTitrationPlots.R .
        """

# -- mask away a few known "bad" areas of the reference (inaccurate
# -- reference, observed nonclonality in sample, etc.)

rule mask_variants:
    input: masked_variant_calls.values()

rule mask_variants_1:
    output: "conditions/{condition}/variant_calling/masked-variants.{depth}.gff"
    input:
        rawVariantsGff="conditions/{condition}/variant_calling/variants.{depth}.gff",
        genomeMask=lambda wc: ct.referenceMask(wc.condition)
    shell:
        """
        module load gfftools/dalexander
        gffsubtract.pl {input.rawVariantsGff} {input.genomeMask} > {output}
        """

# -- summarize true available coverage levels

# TODO: make "otherbin" tools available via "module load smrtanalysis/mainline-with-otherbins
SUMMARIZE_COVERAGE="/pbi/dept/secondary/builds/links/current_develop_smrttools-cleanbuild_installdir/private/otherbins/internalall/bin/summarize_coverage"

rule summarize_coverage:
    output: "conditions/{condition}/variant_calling/alignments-summary.gff"
    input:
        alignments="conditions/{condition}/mapped/mapped.alignmentset.xml",
        reference="conditions/{condition}/reference.fasta"
    shell:
        """
        {SUMMARIZE_COVERAGE} --region_size=10000 {input.alignments} {input.reference} {output}
        """

# -- variant calling at fixed coverage depths;

rule coverage_titration:
    input: variant_calls.values()


def modelPathArg(config):
    if ("bh2.variant_calling.model_path" not in config or
        not config["bh2.variant_calling.model_path"]):
        return ""
    else:
        return "--parametersFile %s" % config["bh2.variant_calling.model_path"]

def modelSpecArg(config):
    if ("bh2.variant_calling.model_spec" not in config or
        not config["bh2.variant_calling.model_spec"]):
        return ""
    else:
        return "--parametersSpec %s" % config["bh2.variant_calling.model_spec"]


rule variant_call_fixed_coverage_one_condition:
    input: "conditions/{condition}/mapped/mapped.alignmentset.xml"
    output:
        gff=  "conditions/{condition}/variant_calling/variants.{depth}.gff",
        fasta="conditions/{condition}/variant_calling/consensus.{depth}.fasta",
        fastq="conditions/{condition}/variant_calling/consensus.{depth}.fastq"
    params:
        algorithm=config.get("bh2.variant_calling.consensus_algorithm", "arrow"),
        modelPathArg=modelPathArg(config),
        modelSpecArg=modelSpecArg(config),
        reference=lambda wc: ct.reference(wc.condition)
    threads: 8
    shell:
        """
        variantCaller {params.modelPathArg} {params.modelSpecArg} --algorithm={params.algorithm} \
             -X{wildcards.depth} -x0 -q0 -j {threads} --reportEffectiveCoverage \
            {input} -r {params.reference} -o {output.gff} \
            -o {output.fasta} -o {output.fastq}
        """
