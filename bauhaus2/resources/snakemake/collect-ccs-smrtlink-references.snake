
# ---------------------------------------------------------------------------------------------------
# collect-smrtlink-references.snake: hotlink "remote" smrtlink reference FASTAs into our workflow directory

local_fasta_files = \
    { c : expand("conditions/{condition}/reference.fasta", condition=c)
      for c in ct.conditions }

remote_sts_h5 = \
    { c : ct.inputsH5(c)
      for c in ct.conditions }
      
local_alignmentset = \
    { c : expand("conditions/{condition}/mapped_ccs/mapped-ccs.alignmentset.xml", condition=c)
      for c in ct.conditions }

rule collect_smrtlink_references:
    input: local_fasta_files.values()

# Here the sts.h5 file is fetched at the same time as the reference, just to simplify the process 
# When more sts or other data files are collected, they should be separated to a new snakemake file
rule collect_smrtlink_reference_one_condition:
    input:
        alignment=lambda wc: local_alignmentset[wc.condition],
        sts=lambda wc: remote_sts_h5[wc.condition]
    output:
        fasta="conditions/{condition}/reference.fasta",
        fai="conditions/{condition}/reference.fasta.fai",
        sts="conditions/{condition}/sts.h5"
    shell:
        """
        module load xpath
        alignment={input.alignment}
        ref="$(xpath -q -e '(//pbbase:ExternalResource[@MetaType="PacBio.ReferenceFile.ReferenceFastaFile"]/@ResourceId)[1]' $alignment | awk -F '[""]' '{{print $2}}')"
        ln -s $ref {output.fasta}
        ref+=".fai"
        ln -s $ref {output.fai}
        ln -s {input.sts} {output.sts}
        """
