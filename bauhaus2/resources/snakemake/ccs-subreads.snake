
# ---------------------------------------------------------------------------------------------------
# ccs-subreads.snake

ccs_chunks = \
    { c : expand("conditions/{condition}/ccs/chunks/ccs.chunk{chunkNo}.consensusreadset.xml",
                 condition=c, chunkNo=range(config["bh2.scatter_subreads.chunks_per_condition"]))
      for c in ct.conditions }

gathered_ccs_chunks = \
    { c : expand("conditions/{condition}/ccs/ccs.consensusreadset.xml", condition=c)
      for c in ct.conditions }

# -- Target rules --

rule ccs:
    input: gathered_ccs_chunks.values()

rule ccs_chunks:
    input: listConcat(ccs_chunks.values())

# -- Worker rules --

rule ccs_one_chunk:
    input:  "conditions/{condition}/subreads/chunks/input.chunk{chunkNo}.subreadset.xml"
    output:
        dset="conditions/{condition}/ccs/chunks/ccs.chunk{chunkNo}.consensusreadset.xml",
        bam= "conditions/{condition}/ccs/chunks/ccs.chunk{chunkNo}.ccs.bam",
        ccsDiagnosticsReport="conditions/{condition}/ccs/chunks/ccs.chunk{chunkNo}.report.txt"
    params:
        modelPath=lambda wc: ct.modelPathArg(wc.condition, "--modelPath"),
        modelSpec=lambda wc: ct.modelSpecArg(wc.condition, "--modelSpec")
    threads: 8
    benchmark: "benchmarks/{condition}_{chunkNo}_ccs_one_chunk.tsv"
    shell:
        """
        ccs --force --numThreads={threads} \
            --reportFile={output.ccsDiagnosticsReport} {params.modelPath} {params.modelSpec} \
             {input} {output.bam} && \
        dataset create --type ConsensusReadSet {output.dset} {output.bam}
        """

rule ccs_one_condition:
    input: lambda wc: ccs_chunks[wc.condition]
    output: "conditions/{condition}/ccs/ccs.consensusreadset.xml"
    shell:
        """
        dataset merge {output} {input}
        """
